CREATE OR REPLACE PROCEDURE Sp_T_Acc_Slip_Body_D
(
	AR_SLIP_ID                                 NUMBER,
	AR_SLIP_IDSEQ                              NUMBER
)
IS
	lnMAKE_SLIPLINE				T_ACC_SLIP_BODY.MAKE_SLIPLINE%TYPE;
	lsACC_CODE					T_ACC_SLIP_BODY.ACC_CODE%TYPE;
	lsACC_NAME					T_ACC_CODE.ACC_NAME%TYPE;
	lsSUMMARY1					T_ACC_SLIP_BODY.SUMMARY1%TYPE;
	lnDB_AMT					T_ACC_SLIP_BODY.DB_AMT%TYPE;
	lnCR_AMT					T_ACC_SLIP_BODY.CR_AMT%TYPE;
	ldKEEP_DT					T_ACC_SLIP_BODY.KEEP_DT%TYPE;
	lnCnt1						T_ACC_SLIP_BODY.SLIP_ID%TYPE;
	lsMsg						VARCHAR2(4000);
	lsErrm						VARCHAR2(2000);
/**************************************************************************/
/* 1. 프 로 그 램 id : SP_T_ACC_SLIP_BODY_D
/* 2. 유형(시나리오) : Procedure
/* 3. 기  능  정  의 : T_ACC_SLIP_BODY 테이블 Delete
/* 4. 변  경  이  력 : 최언회 작성(2005-12-07)
/* 5. 관련  프로그램 :
/* 6. 특  기  사  항 :
/**************************************************************************/
BEGIN
	lsErrm := Pkg_T_Check_Slip.CheckDeleteDetail(Ar_Slip_Id,Ar_Slip_IdSeq);
	IF lsErrm IS NOT NULL THEN
	RAISE_APPLICATION_ERROR(-20009,REPLACE(lsErrm,'ORA-20009:',''));
	END IF;

	SELECT
		A.MAKE_SLIPLINE,
		A.ACC_CODE,
		B.ACC_NAME,
		A.SUMMARY1,
		A.DB_AMT,
		A.CR_AMT,
		A.KEEP_DT
	INTO
		lnMAKE_SLIPLINE,
		lsACC_CODE,
		lsACC_NAME,
		lsSUMMARY1,
		lnDB_AMT,
		lnCR_AMT,
		ldKEEP_DT
	FROM	T_ACC_SLIP_BODY_INS A,
			T_ACC_CODE B
	WHERE	A.ACC_CODE = B.ACC_CODE
	AND		A.SLIP_ID = AR_SLIP_ID
	AND		A.SLIP_IDSEQ = AR_SLIP_IDSEQ;

	lsMsg :=	'<br>계정과목 : '||lsACC_CODE||'('||lsACC_NAME||')<br>'||
				'적요 : '||lsSUMMARY1||'<br>'||
				'차대금액 : '||TO_CHAR(lnDB_AMT,'FM999,999,999,999,990')||' / '||TO_CHAR(lnCR_AMT,'FM999,999,999,999,990');

	IF ldKEEP_DT IS NOT NULL THEN
		lsMsg := '전표오류!!!<br>'||'해당전표는 확정된 전표입니다.<br>전표내역을 삭제할수 없습니다.'||lsMsg;
		RAISE_APPLICATION_ERROR (-20009, lsMsg);
	END IF;
	
	--지급어음설정 어음 CLEAR
	SELECT	COUNT(*)
	INTO	lnCnt1
	FROM	T_FIN_PAY_CHK_BILL
	WHERE	SLIP_ID = AR_SLIP_ID
	AND		SLIP_IDSEQ = AR_SLIP_IDSEQ
	AND		RESET_SLIP_ID IS NOT NULL;

	IF NVL(lnCnt1,0) > 0 THEN
		lsMsg := '전표오류!!!<br>'||'해당지급어음의 반제내역이 존재합니다.<br>해당전표내역을 삭제할수 없습니다.'||lsMsg;
		RAISE_APPLICATION_ERROR (-20009, lsMsg);
	ELSE
		UPDATE T_FIN_PAY_CHK_BILL
		SET
			STAT_CLS = '1',
			PUBL_DT = NULL,
			EXPR_DT = NULL,
			PUBL_AMT = NULL,
			SLIP_ID = NULL,
			SLIP_IDSEQ = NULL
		WHERE	SLIP_ID = AR_SLIP_ID
		AND		SLIP_IDSEQ = AR_SLIP_IDSEQ
		AND		RESET_SLIP_ID IS NULL;
	END IF;

	--지급어음반제 CLEAR
	UPDATE T_FIN_PAY_CHK_BILL
	SET
		RESET_SLIP_ID = NULL,
		RESET_SLIP_IDSEQ = NULL
	WHERE	RESET_SLIP_ID = AR_SLIP_ID
	AND		RESET_SLIP_IDSEQ = AR_SLIP_IDSEQ;

	--받을어음설정 CLEAR
	SELECT	COUNT(*)
	INTO	lnCnt1
	FROM	T_FIN_RECEIVE_CHK_BILL
	WHERE	SLIP_ID = AR_SLIP_ID
	AND		SLIP_IDSEQ = AR_SLIP_IDSEQ
	AND		RESET_SLIP_ID IS NOT NULL;

	IF NVL(lnCnt1,0) > 0 THEN
		lsMsg := '전표생성오류!!!<br>'||'해당받을어음의 반제내역이 존재합니다.<br>해당전표내역을 삭제할수 없습니다.'||lsMsg;
		RAISE_APPLICATION_ERROR (-20009, lsMsg);
	ELSE
		DELETE	T_FIN_RECEIVE_CHK_BILL
		WHERE	SLIP_ID = AR_SLIP_ID
		AND		SLIP_IDSEQ = AR_SLIP_IDSEQ
		AND		RESET_SLIP_ID IS NULL;
	END IF;


	--받을어음반제 CLEAR
	UPDATE T_FIN_RECEIVE_CHK_BILL
	SET
		RESET_AMT = 0,
		RESET_SLIP_ID = NULL,
		RESET_SLIP_IDSEQ = NULL
	WHERE	RESET_SLIP_ID = AR_SLIP_ID
	AND		RESET_SLIP_IDSEQ = AR_SLIP_IDSEQ;

	--유가증권설정 CLEAR
	-- 유가증권 미수이자가 등록되어 있으면 에러
	SELECT	COUNT(*)
	INTO	lnCnt1
	FROM
		T_FIN_SECURTY A,
		T_FIN_SEC_ITR_AMT B
	WHERE
	A.SECU_NO = B.SECU_NO
	AND	A.SLIP_ID = AR_SLIP_ID
	AND	A.SLIP_IDSEQ = AR_SLIP_IDSEQ;

	IF NVL(lnCnt1,0) > 0 THEN
		lsMsg := '전표생성오류!!!<br>'||'해당유가증권의 미수이자가 존재합니다.<br>삭제할수 없습니다.'||lsMsg;
		RAISE_APPLICATION_ERROR (-20009, lsMsg);
	END IF;

	SELECT	COUNT(*)
	INTO	lnCnt1
	FROM	T_FIN_SECURTY
	WHERE	SLIP_ID = AR_SLIP_ID
	AND	SLIP_IDSEQ = AR_SLIP_IDSEQ
	AND	RESET_SLIP_ID IS NOT NULL;

	IF NVL(lnCnt1,0) > 0 THEN
		lsMsg := '전표생성오류!!!<br>'||'해당유가증권의 반제내역이 존재합니다.<br>삭제할수 없습니다.'||lsMsg;
		RAISE_APPLICATION_ERROR (-20009, lsMsg);
	ELSE
		DELETE	T_FIN_SECURTY
		WHERE	SLIP_ID = AR_SLIP_ID
		AND	SLIP_IDSEQ = AR_SLIP_IDSEQ
		AND	RESET_SLIP_ID IS NULL;
	END IF;

	--유가증권반제 CLEAR
	UPDATE T_FIN_SECURTY
	SET
		SALE_AMT = NULL,
		SALE_DT = NULL,
		RETURN_DT = NULL,
		CONSIGN_BANK = NULL,
		SALE_ITR_AMT = NULL,
		SALE_TAX = NULL,
		SALE_LOSS = NULL,
		SALE_NP_ITR_AMT = NULL,
		RESET_SLIP_ID = NULL,
		RESET_SLIP_IDSEQ = NULL
	WHERE	RESET_SLIP_ID = AR_SLIP_ID
	AND	RESET_SLIP_IDSEQ = AR_SLIP_IDSEQ;

	--차입/상환 내역 삭제
	UPDATE
		T_FIN_LOAN_REFUND_LIST
	SET
		LOAN_SLIP_ID = NULL,
		LOAN_SLIP_IDSEQ = NULL
	WHERE
		LOAN_SLIP_ID = AR_SLIP_ID
		AND	LOAN_SLIP_IDSEQ = AR_SLIP_IDSEQ;

	UPDATE
		T_FIN_LOAN_REFUND_LIST
	SET
		REFUND_SLIP_ID = NULL,
		REFUND_SLIP_IDSEQ = NULL
	WHERE
		REFUND_SLIP_ID = AR_SLIP_ID
		AND	REFUND_SLIP_IDSEQ = AR_SLIP_IDSEQ;

	--적금 불입내역 CLEAR
	UPDATE T_DEPOSIT_PAYMENT_LIST
	SET
		PAYMENT_DT = NULL,
		PAYMENT_AMT = 0,
		SLIP_ID = NULL,
		SLIP_IDSEQ = NULL
	WHERE	SLIP_ID = AR_SLIP_ID
	AND		SLIP_IDSEQ = AR_SLIP_IDSEQ;

	--CP매입설정 CLEAR
	SELECT	COUNT(*)
	INTO	lnCnt1
	FROM	T_FIN_CP_BUY
	WHERE	SLIP_ID = AR_SLIP_ID
	AND	SLIP_IDSEQ = AR_SLIP_IDSEQ
	AND	RESET_SLIP_ID IS NOT NULL;

	IF NVL(lnCnt1,0) > 0 THEN
		lsMsg := '전표생성오류!!!<br>'||'해당CP매입의 반제내역이 존재합니다.<br>해당전표내역을 삭제할수 없습니다.'||lsMsg;
		RAISE_APPLICATION_ERROR (-20009, lsMsg);
	ELSE
		DELETE	T_FIN_CP_BUY
		WHERE	SLIP_ID = AR_SLIP_ID
		AND	SLIP_IDSEQ = AR_SLIP_IDSEQ
		AND	RESET_SLIP_ID IS NULL;
	END IF;

	--CP매입반제 CLEAR
	UPDATE T_FIN_CP_BUY
	SET
		RESET_AMT = NULL,
		REMARKS = NULL,
		RESET_SLIP_ID = NULL,
		RESET_SLIP_IDSEQ = NULL
	WHERE	RESET_SLIP_ID = AR_SLIP_ID
	AND	RESET_SLIP_IDSEQ = AR_SLIP_IDSEQ;

	--현금영수증
	DELETE	T_ACC_SLIP_EXPENSE_CASH
	WHERE	SLIP_ID = AR_SLIP_ID
	AND	SLIP_IDSEQ = AR_SLIP_IDSEQ;

	--카드영수증 반제
	DELETE	T_ACC_SLIP_EXPENSE_CARDS
	WHERE	SLIP_ID = AR_SLIP_ID
	AND	SLIP_IDSEQ = AR_SLIP_IDSEQ;

	--상계전표 정보 삭제
	DELETE	T_ACC_SLIP_REMAIN_KEEP
	WHERE	SLIP_ID = AR_SLIP_ID
	AND		SLIP_IDSEQ = AR_SLIP_IDSEQ;

	DELETE	T_ACC_SLIP_REMAIN_KEEP
	WHERE	SLIP_ID = AR_SLIP_ID
	AND		RESET_SLIP_IDSEQ = AR_SLIP_IDSEQ;

	--현금전표추적정보 삭제
	DELETE	T_FL_TRACE_CASH_SLIP
	WHERE	CASH_SLIP_ID = AR_SLIP_ID
	AND		CASH_SLIP_IDSEQ = AR_SLIP_IDSEQ;

	DELETE	T_FL_TRACE_CASH_SLIP
	WHERE	TRACE_SLIP_ID = AR_SLIP_ID
	AND		TRACE_SLIP_IDSEQ = AR_SLIP_IDSEQ;

	--전표BODY 삭제
	DELETE T_ACC_SLIP_BODY_INS
	WHERE	SLIP_ID = AR_SLIP_ID
	AND	SLIP_IDSEQ = AR_SLIP_IDSEQ;
END;
/
